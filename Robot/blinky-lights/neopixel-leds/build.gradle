plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:[4.13.1,5.0.0)'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.12.0'
}

group = 'org.usfirst.frc.team4999'
version = '3.0'
description = 'neopixel-leds'
java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
    repositories {
        maven {
            url = layout.projectDirectory.dir("../../Momentum-Tools/mvn-repo")
        }
    }
}

javadoc {
    source = sourceSets.main.allJava
    failOnError = false
}

publish {
    dependsOn javadoc
}

test {
    finalizedBy jacocoTestReport
    outputs.upToDateWhen{false}
}

jacocoTestReport {
    dependsOn test
}
